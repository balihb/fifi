buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:3.3.4"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE"
    }
}

plugins {
    id 'java-library'
    id 'eclipse'
}

ext { springBootVersion = '2.1.3.RELEASE' }

apply plugin: 'org.openapi.generator'
apply plugin: "io.spring.dependency-management"

sourceSets {
    main {
        java { srcDirs += [ "${buildDir}/generated/src/main/java" ]
        }
    }
    injvm {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    injvm
    injvmImplementation.extendsFrom implementation
}

task injvmJar(type:Jar) {
    archiveBaseName = 'fifi'
    archiveAppendix = 'fib-impl-injvm'
    from sourceSets.injvm.output
}

artifacts {
    injvm injvmJar
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    api project(':fib:fib-api')
    api project(':fib-timer:fib-timer-api')

    testImplementation 'junit:junit:4.12'
}

dependencyManagement {
    imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
}

repositories {
    jcenter()
}

openApiGenerate {
    generatorName = "spring"
    verbose = false
    inputSpec = "${project(':fib-timer:fib-timer-api').sourceSets.main.resources.srcDirs.find{ file("${it}/openapi.yaml").exists() }}/openapi.yaml"
    outputDir = "${buildDir}/generated"
    apiPackage = "balihb.fifi.fib_timer.api"
    invokerPackage = "balihb.fifi.fib_timer.invoker"
    modelPackage = "balihb.fifi.fib_timer.model"
    configOptions = [
        dateLibrary: "java8",
        interfaceOnly: true
        //apiFirst: true
    ]
    systemProperties = [
        apis: "",
        models: "false",
        supportingFiles: 'ApiUtil.java'
    ]
}

compileJava.dependsOn tasks.openApiGenerate
eclipseClasspath.dependsOn tasks.openApiGenerate
